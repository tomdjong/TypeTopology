Tom de Jong, 3 - 5 March 2020

As suggested by Martin Escardo.

The (usual) order on the dyadic rationals

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import Dyadic
open import UF-Subsingletons

\end{code}

We inductively define an order â‰º on ğ”» and prove that it is transitive and
linear.

We also show that is is "dense" and "has no endpoints", but formulated using Î£
rather than âˆƒ. Although the proper formulation would use âˆƒ (to ensure that being
dense and having no endpoints is property, rather than structure), we still
prove the Î£-versions for two reasons:
- we can easily prove them and derive the âˆƒ-versions from them;
- so that this module does not depend on propositional truncation.

\begin{code}

module Dyadic-Order where

\end{code}

We defined â‰º by using the translation (from ğ”» to (-1,1)) as set out in Dyadic.

\begin{code}

_â‰º_ : ğ”» â†’ ğ”» â†’ ğ“¤â‚€ Ì‡
midpoint â‰º midpoint = ğŸ˜
midpoint â‰º left y   = ğŸ˜
midpoint â‰º right y  = ğŸ™
left x   â‰º midpoint = ğŸ™
left x   â‰º left y   = x â‰º y
left x   â‰º right y  = ğŸ™
right x  â‰º midpoint = ğŸ˜
right x  â‰º left y   = ğŸ˜
right x  â‰º right y  = x â‰º y

\end{code}

Monotonicity of left and right holds by definition (so we will never call these
lemmas), but we record them here for clarity.

\begin{code}

left-monotone : {x y : ğ”»} â†’ x â‰º y â†’ left x â‰º left y
left-monotone = id

right-monotone : {x y : ğ”»} â†’ x â‰º y â†’ right x â‰º right y
right-monotone = id

\end{code}

\begin{code}

â‰º-is-prop-valued : (x y : ğ”») â†’ is-prop (x â‰º y)
â‰º-is-prop-valued midpoint  midpoint  = ğŸ˜-is-prop
â‰º-is-prop-valued midpoint  (left y)  = ğŸ˜-is-prop
â‰º-is-prop-valued midpoint  (right y) = ğŸ™-is-prop
â‰º-is-prop-valued (left x)  midpoint  = ğŸ™-is-prop
â‰º-is-prop-valued (left x)  (left y)  = â‰º-is-prop-valued x y
â‰º-is-prop-valued (left x)  (right y) = ğŸ™-is-prop
â‰º-is-prop-valued (right x) midpoint  = ğŸ˜-is-prop
â‰º-is-prop-valued (right x) (left y)  = ğŸ˜-is-prop
â‰º-is-prop-valued (right x) (right y) = â‰º-is-prop-valued x y

â‰º-is-transitive : (x y z : ğ”») â†’ x â‰º y â†’ y â‰º z â†’ x â‰º z
â‰º-is-transitive midpoint midpoint z = ğŸ˜-induction
â‰º-is-transitive midpoint (left y) midpoint = ğŸ˜-induction
â‰º-is-transitive midpoint (left y) (left z) = ğŸ˜-induction
â‰º-is-transitive midpoint (left y) (right z) = ğŸ˜-induction
â‰º-is-transitive midpoint (right y) midpoint _ = id
â‰º-is-transitive midpoint (right y) (left z) _ = id
â‰º-is-transitive midpoint (right y) (right z) _ _ = *
â‰º-is-transitive (left x) midpoint midpoint _ _ = *
â‰º-is-transitive (left x) midpoint (left z) _ = ğŸ˜-induction
â‰º-is-transitive (left x) midpoint (right z) _ = id
â‰º-is-transitive (left x) (left y) midpoint _ = id
â‰º-is-transitive (left x) (left y) (left z) = â‰º-is-transitive x y z
â‰º-is-transitive (left x) (left y) (right z) _ = id
â‰º-is-transitive (left x) (right y) midpoint _ _ = *
â‰º-is-transitive (left x) (right y) (left z) _ = ğŸ˜-induction
â‰º-is-transitive (left x) (right y) (right z) _ _ = *
â‰º-is-transitive (right x) midpoint z = ğŸ˜-induction
â‰º-is-transitive (right x) (left y) z = ğŸ˜-induction
â‰º-is-transitive (right x) (right y) midpoint _ = id
â‰º-is-transitive (right x) (right y) (left z) _ = id
â‰º-is-transitive (right x) (right y) (right z) = â‰º-is-transitive x y z

â‰º-is-linear : (x y : ğ”») â†’ x â‰¢ y â†’ x â‰º y + y â‰º x
â‰º-is-linear midpoint midpoint p = ğŸ˜-induction (p refl)
â‰º-is-linear midpoint (left y) _ = inr *
â‰º-is-linear midpoint (right y) _ = inl *
â‰º-is-linear (left x) midpoint _ = inl *
â‰º-is-linear (left x) (left y) lxâ‰¢ly = â‰º-is-linear x y xâ‰¢y
 where
  xâ‰¢y : x â‰¢ y
  xâ‰¢y = contrapositive (ap left) lxâ‰¢ly
â‰º-is-linear (left x) (right y) _ = inl *
â‰º-is-linear (right x) midpoint _ = inr *
â‰º-is-linear (right x) (left y) _ = inr *
â‰º-is-linear (right x) (right y) rxâ‰¢ry = â‰º-is-linear x y xâ‰¢y
 where
  xâ‰¢y : x â‰¢ y
  xâ‰¢y = contrapositive (ap right) rxâ‰¢ry

\end{code}

Discreteness of ğ”» and linearity of â‰º imply that â‰º is trichotomous, i.e. for
every x y : ğ”» , x â‰º y or x â‰¡ y or y â‰º x holds. The lemmas after
â‰º-is-trichotomous show that exactly one of these is the case, as witnessed by
trichotomy-is-a-singleton.

\begin{code}

â‰º-is-trichotomous : (x y : ğ”») â†’ x â‰º y + (x â‰¡ y) + (y â‰º x)
â‰º-is-trichotomous x y = cases a b (ğ”»-is-discrete x y)
 where
  a : x â‰¡ y â†’ (x â‰º y) + (x â‰¡ y) + (y â‰º x)
  a = inr âˆ˜ inl
  b : (x â‰¢ y) â†’ (x â‰º y) + (x â‰¡ y) + (y â‰º x)
  b xâ‰¢y = cases c d (â‰º-is-linear x y xâ‰¢y)
   where
    c : x â‰º y â†’ (x â‰º y) + (x â‰¡ y) + (y â‰º x)
    c = inl
    d : y â‰º x â†’ (x â‰º y) + (x â‰¡ y) + (y â‰º x)
    d = inr âˆ˜ inr

â‰º-to-â‰¢ : {x y : ğ”»} â†’ x â‰º y â†’ x â‰¢ y
â‰º-to-â‰¢ {midpoint} {midpoint}    = ğŸ˜-induction
â‰º-to-â‰¢ {midpoint} {left y}      = ğŸ˜-induction
â‰º-to-â‰¢ {midpoint} {right y} _   = midpoint-is-not-right
â‰º-to-â‰¢ {left x}   {midpoint} _  = (Î» p â†’ midpoint-is-not-left (p â»Â¹))
â‰º-to-â‰¢ {left x}   {left y} xâ‰ºy  = contrapositive left-lc (â‰º-to-â‰¢ xâ‰ºy)
â‰º-to-â‰¢ {left x}   {right y} _   = left-is-not-right
â‰º-to-â‰¢ {right x}  {midpoint}    = ğŸ˜-induction
â‰º-to-â‰¢ {right x}  {left y}      = ğŸ˜-induction
â‰º-to-â‰¢ {right x}  {right y} xâ‰ºy = contrapositive right-lc (â‰º-to-â‰¢ xâ‰ºy)

â‰º-to-â‰¢' : {x y : ğ”»} â†’ y â‰º x â†’ x â‰¢ y
â‰º-to-â‰¢' yâ‰ºx p = â‰º-to-â‰¢ yâ‰ºx (p â»Â¹)

â‰¡-to-Â¬â‰º : {x y : ğ”»} â†’ x â‰¡ y â†’ Â¬ (x â‰º y)
â‰¡-to-Â¬â‰º xâ‰¡y xâ‰ºy = â‰º-to-â‰¢ xâ‰ºy xâ‰¡y

â‰¡-to-Â¬â‰º' : {x y : ğ”»} â†’ x â‰¡ y â†’ Â¬ (y â‰º x)
â‰¡-to-Â¬â‰º' xâ‰¡y yâ‰ºx = â‰º-to-â‰¢ yâ‰ºx (xâ‰¡y â»Â¹)

â‰º-to-Â¬â‰º : (x y : ğ”») â†’ x â‰º y â†’ Â¬ (y â‰º x)
â‰º-to-Â¬â‰º midpoint midpoint    = ğŸ˜-induction
â‰º-to-Â¬â‰º midpoint (left y)    = ğŸ˜-induction
â‰º-to-Â¬â‰º midpoint (right y) _ = id
â‰º-to-Â¬â‰º (left x) midpoint _  = id
â‰º-to-Â¬â‰º (left x) (left y)    = â‰º-to-Â¬â‰º x y
â‰º-to-Â¬â‰º (left x) (right y) _ = id
â‰º-to-Â¬â‰º (right x) midpoint   = ğŸ˜-induction
â‰º-to-Â¬â‰º (right x) (left y)   = ğŸ˜-induction
â‰º-to-Â¬â‰º (right x) (right y)  = â‰º-to-Â¬â‰º x y

trichotomy-is-a-singleton : {x y : ğ”»} â†’ is-singleton (x â‰º y + (x â‰¡ y) + y â‰º x)
trichotomy-is-a-singleton {x} {y} =
 pointed-props-are-singletons (â‰º-is-trichotomous x y) Î³
  where
   Î³ : is-prop (x â‰º y + (x â‰¡ y) + y â‰º x)
   Î³ = +-is-prop (â‰º-is-prop-valued x y) h g
    where
     h : is-prop ((x â‰¡ y) + y â‰º x)
     h = +-is-prop ğ”»-is-a-set (â‰º-is-prop-valued y x) â‰¡-to-Â¬â‰º'
     g : x â‰º y â†’ Â¬ ((x â‰¡ y) + y â‰º x)
     g xâ‰ºy = cases a b
      where
       a : x â‰¢ y
       a = â‰º-to-â‰¢ xâ‰ºy
       b : Â¬ (y â‰º x)
       b = â‰º-to-Â¬â‰º x y xâ‰ºy

\end{code}

Next, we prove that â‰º has no endpoints and is dense (although formulated with Î£,
as explained at the top of this file).

\begin{code}

left-â‰º : (x : ğ”») â†’ left x â‰º x
left-â‰º midpoint = *
left-â‰º (left x) = left-â‰º x
left-â‰º (right x) = *

â‰º-right : (x : ğ”») â†’ x â‰º right x
â‰º-right midpoint = *
â‰º-right (left x) = *
â‰º-right (right x) = â‰º-right x

â‰º-has-no-left-endpoint-Î£ : (x : ğ”») â†’ Î£ y ê‰ ğ”» , y â‰º x
â‰º-has-no-left-endpoint-Î£ x = left x , left-â‰º x

â‰º-has-no-right-endpoint-Î£ : (x : ğ”») â†’ Î£ y ê‰ ğ”» , x â‰º y
â‰º-has-no-right-endpoint-Î£ x = right x , â‰º-right x

â‰º-is-dense-Î£ : (x y : ğ”») â†’ x â‰º y â†’ Î£ z ê‰ ğ”» , x â‰º z Ã— z â‰º y
â‰º-is-dense-Î£ midpoint (right y) _ = right (left y) , * , left-â‰º y
â‰º-is-dense-Î£ (left x) midpoint _ = left (right x) , â‰º-right x , *
â‰º-is-dense-Î£ (left x) (left y) xâ‰ºy = Î³ (â‰º-is-dense-Î£ x y xâ‰ºy)
 where
  Î³ : (Î£ z ê‰ ğ”» , x â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , left x â‰º z Ã— z â‰º left y
  Î³ (z , xâ‰ºz , zâ‰ºy) = left z , xâ‰ºz , zâ‰ºy
â‰º-is-dense-Î£ (left x) (right y) _ = midpoint , * , *
â‰º-is-dense-Î£ (right x) midpoint = ğŸ˜-induction
â‰º-is-dense-Î£ (right x) (left y) = ğŸ˜-induction
â‰º-is-dense-Î£ (right x) (right y) xâ‰ºy = Î³ (â‰º-is-dense-Î£ x y xâ‰ºy)
 where
  Î³ : (Î£ z ê‰ ğ”» , x â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , right x â‰º z Ã— z â‰º right y
  Î³ (z , xâ‰ºz , zâ‰ºy) = right z , xâ‰ºz , zâ‰ºy

\end{code}

Binary interpolation is a generalisation of density, which can, in our case, be
proved from density using trichotomy of â‰º.

We will need this property to construct the (rounded) ideal completion of
(ğ”» , â‰º).

\begin{code}

â‰º-interpolationâ‚‚-Î£ : (xâ‚€ xâ‚ y : ğ”») â†’ xâ‚€ â‰º y â†’ xâ‚ â‰º y
                   â†’ Î£ z ê‰ ğ”» , xâ‚€ â‰º z Ã— xâ‚ â‰º z Ã— z â‰º y
â‰º-interpolationâ‚‚-Î£ xâ‚€ xâ‚ y xâ‚€â‰ºy xâ‚â‰ºy = casesâ‚ƒ a b c (â‰º-is-trichotomous xâ‚€ xâ‚)
 where
  a : xâ‚€ â‰º xâ‚ â†’ Î£ z ê‰ ğ”» , xâ‚€ â‰º z Ã— xâ‚ â‰º z Ã— z â‰º y
  a xâ‚€â‰ºxâ‚ = Î³ (â‰º-is-dense-Î£ xâ‚ y xâ‚â‰ºy)
   where
    Î³ : (Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , xâ‚€ â‰º z Ã— xâ‚ â‰º z Ã— z â‰º y
    Î³ (z , xâ‚â‰ºz , zâ‰ºy) = z , â‰º-is-transitive xâ‚€ xâ‚ z xâ‚€â‰ºxâ‚ xâ‚â‰ºz , xâ‚â‰ºz , zâ‰ºy
  b : xâ‚€ â‰¡ xâ‚ â†’ Î£ z ê‰ ğ”» , xâ‚€ â‰º z Ã— xâ‚ â‰º z Ã— z â‰º y
  b refl = Î³ (â‰º-is-dense-Î£ xâ‚ y xâ‚â‰ºy)
   where
    Î³ : (Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , xâ‚€ â‰º z Ã— xâ‚ â‰º z Ã— z â‰º y
    Î³ (z , xâ‚â‰ºz , zâ‰ºy) = z , xâ‚â‰ºz , xâ‚â‰ºz , zâ‰ºy
  c : xâ‚ â‰º xâ‚€ â†’ Î£ z ê‰ ğ”» , xâ‚€ â‰º z Ã— xâ‚ â‰º z Ã— z â‰º y
  c xâ‚â‰ºxâ‚€ = Î³ (â‰º-is-dense-Î£ xâ‚€ y xâ‚€â‰ºy)
   where
    Î³ : (Î£ z ê‰ ğ”» , xâ‚€ â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , xâ‚€ â‰º z Ã— xâ‚ â‰º z Ã— z â‰º y
    Î³ (z , xâ‚€â‰ºz , zâ‰ºy) = z , xâ‚€â‰ºz , â‰º-is-transitive xâ‚ xâ‚€ z xâ‚â‰ºxâ‚€ xâ‚€â‰ºz , zâ‰ºy

\end{code}
