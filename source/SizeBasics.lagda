Tom de Jong, 10 February 2020 -

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module SizeBasics where

open import SpartanMLTT

open import UF-Base hiding (_â‰ˆ_)
open import UF-Embeddings
open import UF-Equiv
open import UF-EquivalenceExamples
open import UF-PropTrunc hiding (_â‰ˆ_)
open import UF-Retracts
open import UF-UA-FunExt
open import UF-Univalence

open import UF-Size

\end{code}

\begin{code}

singleton-has-size : {ğ“¥ : Universe} {X : ğ“¤ Ì‡ }
                   â†’ is-singleton X
                   â†’ X has-size ğ“¥
singleton-has-size i = (ğŸ™ , singleton-â‰ƒ-ğŸ™' i)

\end{code}

We consider some resizing principles and prove that they are all equivalent
propositional resizing.

\begin{code}

subtype-resizing : (ğ“¤ ğ“¥ : Universe) â†’ ğ“¤ âº âŠ” (ğ“¥ âº) Ì‡
subtype-resizing ğ“¤ ğ“¥ = (X : ğ“¤ Ì‡ ) (P : X â†’ ğ“¥ Ì‡ )
                     â†’ ((x : X) â†’ is-prop (P x))
                     â†’ (Î£ x ê‰ X , P x) has-size ğ“¤

Subtype-resizing : ğ“¤Ï‰
Subtype-resizing = {ğ“¤ ğ“¥ : Universe} â†’ subtype-resizing ğ“¤ ğ“¥

Subtype-resizing-implies-Propositional-resizing : Subtype-resizing
                                                â†’ Propositional-resizing
Subtype-resizing-implies-Propositional-resizing Sr {ğ“¤} {ğ“¥} P i = Q , Î³
 where
  sr : (ğŸ™{ğ“¥} Ã— P) has-size ğ“¥
  sr = Sr (ğŸ™{ğ“¥}) (Î» _ â†’ P) (Î» _ â†’ i)
  Q : ğ“¥ Ì‡
  Q = has-size-type sr
  Î³ = Q     â‰ƒâŸ¨ has-size-equiv sr âŸ©
      ğŸ™ Ã— P â‰ƒâŸ¨ ğŸ™-lneutral âŸ©
      P     â– 

Propositional-resizing-implies-Subtype-resizing : Propositional-resizing
                                                â†’ Subtype-resizing
Propositional-resizing-implies-Subtype-resizing Pr {ğ“¤} {ğ“¥} X P i =
 (Î£ x ê‰ X , Q x) , Î³
  where
   pr : (x : X) â†’ (P x) has-size ğ“¤
   pr x = Pr (P x) (i x)
   Q : X â†’ ğ“¤ Ì‡
   Q x = resize Pr (P x) (i x)
   Î³ : (Î£ x ê‰ X , Q x) â‰ƒ (Î£ x ê‰ X , P x)
   Î³ = Î£-cong (Î» (x : X) â†’ has-size-equiv (pr x))

module _
        (pt : propositional-truncations-exist)
       where
 open import UF-ImageAndSurjection
 open ImageAndSurjection pt
 open PropositionalTruncation pt

 image-resizing-codomain : (ğ“¤ ğ“¥ : Universe) â†’ ğ“¤ âº âŠ” (ğ“¥ âº) Ì‡
 image-resizing-codomain ğ“¤ ğ“¥ = (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ )
                           â†’ (f : X â†’ Y)
                           â†’ image f has-size ğ“¥

 Image-resizing-codomain : ğ“¤Ï‰
 Image-resizing-codomain = {ğ“¤ ğ“¥ : Universe} â†’ image-resizing-codomain ğ“¤ ğ“¥

 Image-resizing-codomain-implies-Propositional-resizing : Image-resizing-codomain
                                                        â†’ Propositional-resizing
 Image-resizing-codomain-implies-Propositional-resizing Ir {ğ“¤} {ğ“¥} P s = Q , Î³
  where
   ir : image unique-to-ğŸ™ has-size ğ“¥
   ir = Ir P (ğŸ™{ğ“¥}) unique-to-ğŸ™
   Q : ğ“¥ Ì‡
   Q = has-size-type ir
   Î³ = Q                           â‰ƒâŸ¨ has-size-equiv ir âŸ©
       image unique-to-ğŸ™           â‰ƒâŸ¨ â‰ƒ-refl (image unique-to-ğŸ™) âŸ©
       (Î£ u ê‰ ğŸ™ , âˆƒ p ê‰ P , * â‰¡ u) â‰ƒâŸ¨ i âŸ©
       (Î£ u ê‰ ğŸ™ , Î£ p ê‰ P , * â‰¡ u) â‰ƒâŸ¨ â‰ƒ-refl _ âŸ©
       (Î£ u ê‰ ğŸ™ , P Ã— (* â‰¡ u))     â‰ƒâŸ¨ Î£-flip âŸ©
       P Ã— (Î£ u ê‰ ğŸ™ , * â‰¡ u)       â‰ƒâŸ¨ ii âŸ©
       P Ã— ğŸ™{ğ“¥}                    â‰ƒâŸ¨ ğŸ™-rneutral âŸ©
       P                           â– 
    where
     i  = Î£-cong (Î» u â†’ a-prop-is-equivalent-to-its-truncation (Ïƒ u))
      where
       Ïƒ : (u : ğŸ™) â†’ is-prop (Î£ p ê‰ P , * â‰¡ u)
       Ïƒ _ = Î£-is-prop s (Î» _ â†’ props-are-sets ğŸ™-is-prop)
     ii = Ã—cong (â‰ƒ-refl P) (singleton-â‰ƒ-ğŸ™ (singleton-types-are-singletons *))

 Propositional-resizing-implies-Image-resizing-codomain : Propositional-resizing
                                                        â†’ Image-resizing-codomain
 Propositional-resizing-implies-Image-resizing-codomain Pr {ğ“¤} {ğ“¥} X Y f =
  Propositional-resizing-implies-Subtype-resizing Pr Y S (Î» y â†’ âˆ¥âˆ¥-is-a-prop)
   where
    S : Y â†’ ğ“¤ âŠ” ğ“¥ Ì‡
    S y = âˆƒ x ê‰ X , f x â‰¡ y

\end{code}

Here are some additional resizing principles, for which I have not been able
that they are equivalent to propositional resizing (or Î©-Resizing), but they
are/seem related.

\begin{code}

module _
        (pt : propositional-truncations-exist)
        (fe : FunExt)
        (pe : PropExt)
       where

 open import UF-Quotient
 open Quotient pt fe

 open import UF-ImageAndSurjection hiding (_â‰ˆ_)
 open ImageAndSurjection pt
 open PropositionalTruncation pt

 quotient-resizing : (ğ“¤ ğ“¥ : Universe) â†’ ğ“¤ âº âŠ” (ğ“¥ âº) Ì‡
 quotient-resizing ğ“¤ ğ“¥ = (X : ğ“¤ Ì‡ ) (_â‰ˆ_ : X â†’ X â†’ ğ“¥ Ì‡ )
                         (â‰ˆp : is-prop-valued _â‰ˆ_)
                         (â‰ˆr : reflexive _â‰ˆ_)
                         (â‰ˆs : symmetric _â‰ˆ_)
                         (â‰ˆt : transitive _â‰ˆ_)
                       â†’ (X/â‰ˆ (pe ğ“¥) X _â‰ˆ_ â‰ˆp â‰ˆr â‰ˆs â‰ˆt) has-size ğ“¤

 Quotient-resizing : ğ“¤Ï‰
 Quotient-resizing = {ğ“¤ ğ“¥ : Universe} â†’ quotient-resizing ğ“¤ ğ“¥

 image-resizing-domain : (ğ“¤ ğ“¥ : Universe) â†’ ğ“¤ âº âŠ” (ğ“¥ âº) Ì‡
 image-resizing-domain ğ“¤ ğ“¥ = (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ ) (f : X â†’ Y)
                           â†’ image f has-size ğ“¤

 Image-resizing-domain : ğ“¤Ï‰
 Image-resizing-domain = {ğ“¤ ğ“¥ : Universe} â†’ image-resizing-domain ğ“¤ ğ“¥

 surjective-resizing : (ğ“¤ ğ“¥ : Universe) â†’ ğ“¤ âº âŠ” (ğ“¥ âº) Ì‡
 surjective-resizing ğ“¤ ğ“¥ = (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ ) (f : X â†’ Y)
                         â†’ is-surjection f
                         â†’ Y has-size ğ“¤

 Surjective-resizing : ğ“¤Ï‰
 Surjective-resizing = {ğ“¤ ğ“¥ : Universe} â†’ surjective-resizing ğ“¤ ğ“¥

 Image-resizing-domain-implies-Quotient-resizing : Image-resizing-domain
                                                 â†’ Quotient-resizing
 Image-resizing-domain-implies-Quotient-resizing Ir {ğ“¤} {ğ“¥} X _â‰ˆ_ â‰ˆp â‰ˆr â‰ˆs â‰ˆt =
  Ir X (X â†’ Î© ğ“¥) (equiv-rel (pe ğ“¥) X _â‰ˆ_ â‰ˆp â‰ˆr â‰ˆs â‰ˆt)

 Image-resizing-domain-implies-Surjective-resizing : Image-resizing-domain
                                                   â†’ Surjective-resizing
 Image-resizing-domain-implies-Surjective-resizing Ir {ğ“¤} {ğ“¥} X Y f s = Z , Î³
  where
   ir : image f has-size ğ“¤
   ir = Ir X Y f
   Z : ğ“¤ Ì‡
   Z = has-size-type ir
   Î³ = Z       â‰ƒâŸ¨ has-size-equiv ir âŸ©
       image f â‰ƒâŸ¨ surjection-â‰ƒ-image f s âŸ©
       Y       â– 

 Surjective-resizing-implies-Image-resizing-domain : Surjective-resizing
                                                   â†’ Image-resizing-domain
 Surjective-resizing-implies-Image-resizing-domain Sr {ğ“¤} {ğ“¥} X Y f =
  Sr X (image f) (corestriction f) (corestriction-surjection f)

 Î©-Resizing-implies-quotient-resizing : {ğ“¤ ğ“¥ : Universe}
                                      â†’ Î©-Resizing ğ“¥ ğ“¤ â†’ quotient-resizing ğ“¤ ğ“¥
 Î©-Resizing-implies-quotient-resizing {ğ“¤} {ğ“¥} Î©R X _â‰ˆ_ â‰ˆp â‰ˆr â‰ˆs â‰ˆt =
  (image _â‰‹'_) , Î³
   where
    _â‰‹_ : X â†’ X â†’ Î© ğ“¥
    x â‰‹ y = x â‰ˆ y , â‰ˆp x y
    Î©' : ğ“¤ Ì‡
    Î©' = has-size-type Î©R
    e : Î©' â‰ƒ Î© ğ“¥
    e = has-size-equiv Î©R
    _â‰‹'_ : X â†’ X â†’ Î©'
    x â‰‹' y = back-eqtofun e (x â‰‹ y)
    fe' : {ğ“¤ ğ“¥ : Universe} â†’ funext ğ“¤ ğ“¥
    fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥
    Î³ : image _â‰‹'_ â‰ƒ image _â‰‹_
    Î³ = image _â‰‹'_                                   â‰ƒâŸ¨ â‰ƒ-refl _ âŸ©
        (Î£ Î± ê‰ (X â†’ Î©') , âˆƒ x ê‰ X , _â‰‹'_ x â‰¡ Î±)      â‰ƒâŸ¨ I âŸ©
        (Î£ Î± ê‰ (X â†’ Î©') , âˆƒ x ê‰ X , _â‰‹_ x â‰¡ âŒœ Ï• âŒ Î±) â‰ƒâŸ¨ II âŸ©
        image _â‰‹_                                    â– 
     where
      Ï• : (X â†’ Î©') â‰ƒ (X â†’ Î© ğ“¥)
      Ï• = â†’cong (fe') (fe') (â‰ƒ-refl X) e
      II = Î£-change-of-variables (Î» (Î± : X â†’ Î© ğ“¥) â†’ âˆƒ x ê‰ X , _â‰‹_ x â‰¡ Î±)
           âŒœ Ï• âŒ (âŒœâŒ-is-equiv Ï•)
      I = Î£-cong h
       where
        h : (Î± : X â†’ Î©')
          â†’ (âˆƒ x ê‰ X , _â‰‹'_ x â‰¡ Î±) â‰ƒ (âˆƒ x ê‰ X , _â‰‹_ x â‰¡ âŒœ Ï• âŒ Î±)
        h Î± = logically-equivalent-props-are-equivalent
              âˆ¥âˆ¥-is-a-prop âˆ¥âˆ¥-is-a-prop f g
         where
          f : (âˆƒ x ê‰ X , _â‰‹'_ x â‰¡ Î±) â†’ (âˆƒ x ê‰ X , _â‰‹_ x â‰¡ âŒœ Ï• âŒ Î±)
          f = âˆ¥âˆ¥-functor Ïˆ
           where
            Ïˆ : (Î£ x ê‰ X , _â‰‹'_ x â‰¡ Î±) â†’ (Î£ x ê‰ X , _â‰‹_ x â‰¡ âŒœ Ï• âŒ Î±)
            Ïˆ (x , u) = x , v
             where
              v = _â‰‹_ x                          â‰¡âŸ¨ i âŸ©
                  âŒœ e âŒ âˆ˜ back-eqtofun e âˆ˜ _â‰‹_ x â‰¡âŸ¨ refl âŸ©
                  âŒœ e âŒ âˆ˜ (_â‰‹'_ x)               â‰¡âŸ¨ ap (Î» - â†’ âŒœ e âŒ âˆ˜ -) u âŸ©
                  âŒœ e âŒ âˆ˜ Î±                      â‰¡âŸ¨ refl âŸ©
                  âŒœ Ï• âŒ Î±                        âˆ
               where
                i = ap (Î» - â†’ - âˆ˜ _â‰‹_ x)
                    (dfunext fe' (inverse-is-section âŒœ e âŒ (âŒœâŒ-is-equiv e))) â»Â¹
          g : (âˆƒ x ê‰ X , _â‰‹_ x â‰¡ âŒœ Ï• âŒ Î±) â†’ (âˆƒ x ê‰ X , _â‰‹'_ x â‰¡ Î±)
          g = âˆ¥âˆ¥-functor Ïˆ
           where
            Ïˆ : (Î£ x ê‰ X , _â‰‹_ x â‰¡ âŒœ Ï• âŒ Î±) â†’ (Î£ x ê‰ X , _â‰‹'_ x â‰¡ Î±)
            Ïˆ (x , u) = x , v
             where
              v = _â‰‹'_ x                     â‰¡âŸ¨ refl âŸ©
                  back-eqtofun e âˆ˜ _â‰‹_ x     â‰¡âŸ¨ ap (Î» - â†’ back-eqtofun e âˆ˜ -) u âŸ©
                  back-eqtofun e âˆ˜ âŒœ Ï• âŒ Î±   â‰¡âŸ¨ refl âŸ©
                  back-eqtofun e âˆ˜ âŒœ e âŒ âˆ˜ Î± â‰¡âŸ¨ i âŸ©
                  Î±                          âˆ
               where
                i = ap (Î» - â†’ - âˆ˜ Î±)
                    (dfunext fe' (inverse-is-retraction âŒœ e âŒ (âŒœâŒ-is-equiv e)))

\end{code}

We prove for sets that having a certain size is closed under retracts.

We can prove the same thing for (higher) types if the section is additionally
assumed to be an embedding.

Note that for sets this extra condition is automatically true. However, we keep
the specific construction for sets, because, unlike the general construction, it
does not require propositional truncation.

\begin{code}

fiber-of-section-to-a-set : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                          â†’ is-set Y
                          â†’ (s : X â†’ Y)
                          â†’ (Ï : is-section s)
                          â†’ (y : Y) â†’ (fiber s y) â‰ƒ (s (prâ‚ Ï y) â‰¡ y)
fiber-of-section-to-a-set Ïƒ s (r , Ï) y = qinveq f (g , (gf , fg))
 where
  f : fiber s y â†’ s (r y) â‰¡ y
  f (x , p) = s (r y)     â‰¡âŸ¨ ap (s âˆ˜ r) (p â»Â¹) âŸ©
              s (r (s x)) â‰¡âŸ¨ ap s (Ï x) âŸ©
              s x         â‰¡âŸ¨ p âŸ©
              y           âˆ
  g : s (r y) â‰¡ y â†’ fiber s y
  g q = (r y) , q
  gf : (w : fiber s y) â†’ g (f w) â‰¡ w
  gf (x , refl) = to-subtype-â‰¡ (Î» _ â†’ Ïƒ) (Ï x)
  fg : (q : s (r y) â‰¡ y) â†’ f (g q) â‰¡ q
  fg q = Ïƒ (f (g q)) q

fixed-points-of-section-retraction-to-set : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                                          â†’ is-set Y
                                          â†’ (Ï : X â— Y)
                                          â†’ (Î£ y ê‰ Y ,
                                             section Ï (retraction Ï y) â‰¡ y)
                                          â‰ƒ X
fixed-points-of-section-retraction-to-set {ğ“¤} {ğ“¥} {X} {Y} i (r , s , Ï) =
 (Î£ y ê‰ Y , s (r y) â‰¡ y) â‰ƒâŸ¨ Î³ âŸ©
 (Î£ y ê‰ Y , (fiber s y)) â‰ƒâŸ¨ â‰ƒ-sym (sum-of-fibers X Y s) âŸ©
 X                       â– 
  where
   Î³ = Î£-cong (Î» (y : Y) â†’ â‰ƒ-sym (fiber-of-section-to-a-set i s (r , Ï) y))

retract-of-a-set-has-size : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                          â†’ is-set Y
                          â†’ X â— Y
                          â†’ X has-size ğ“¥
retract-of-a-set-has-size {ğ“¤} {ğ“¥} {X} {Y} i (r , s , Ï) =
 (Î£ y ê‰ Y , s (r y) â‰¡ y) ,
 fixed-points-of-section-retraction-to-set i (r , s , Ï)

module _ (pt : propositional-truncations-exist) where
 open PropositionalTruncation pt

 fiber-of-section-embedding : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                            â†’ (s : X â†’ Y)
                            â†’ (Ï : is-section s)
                            â†’ is-embedding s
                            â†’ (y : Y) â†’ fiber s y â‰ƒ âˆ¥ s (prâ‚ Ï y) â‰¡ y âˆ¥
 fiber-of-section-embedding s (r , Ï) Îµ y =
  logically-equivalent-props-are-equivalent (Îµ y) âˆ¥âˆ¥-is-a-prop f g
   where
    f : fiber s y â†’ âˆ¥ s (r y) â‰¡ y âˆ¥
    f (x , refl) = âˆ£ ap s (Ï x) âˆ£
    g : âˆ¥ s (r y) â‰¡ y âˆ¥ â†’ fiber s y
    g = âˆ¥âˆ¥-rec (Îµ y) h
     where
      h : s (r y) â‰¡ y â†’ fiber s y
      h q = (r y) , q

 fixed-points-of-embedding-retraction : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                                      â†’ (Ï : X â— Y)
                                      â†’ is-embedding (section Ï)
                                      â†’ (Î£ y ê‰ Y ,
                                         âˆ¥ section Ï (retraction Ï y) â‰¡ y âˆ¥)
                                      â‰ƒ X
 fixed-points-of-embedding-retraction {ğ“¤} {ğ“¥} {X} {Y} (r , s , Ï) Îµ =
  (Î£ y ê‰ Y , âˆ¥ s (r y) â‰¡ y âˆ¥) â‰ƒâŸ¨ h âŸ©
  (Î£ y ê‰ Y , fiber s y)       â‰ƒâŸ¨ â‰ƒ-sym (sum-of-fibers X Y s) âŸ©
  X                           â– 
   where
    h = Î£-cong (Î» (y : Y) â†’ â‰ƒ-sym (fiber-of-section-embedding s (r , Ï) Îµ y))

 fiber-of-section-to-a-set' : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                            â†’ is-set Y
                            â†’ (s : X â†’ Y)
                            â†’ (Ï : is-section s)
                            â†’ (y : Y) â†’ (fiber s y) â‰ƒ (s (prâ‚ Ï y) â‰¡ y)
 fiber-of-section-to-a-set' Ïƒ s (r , Ï) y =
  fiber s y       â‰ƒâŸ¨ fiber-of-section-embedding s (r , Ï) Îµ y âŸ©
  âˆ¥ s (r y) â‰¡ y âˆ¥ â‰ƒâŸ¨ a-prop-is-equivalent-to-its-truncation Ïƒ âŸ©
  (s (r y) â‰¡ y)   â– 
   where
    Îµ = lc-maps-into-sets-are-embeddings s (sections-are-lc s ((r , Ï))) Ïƒ

 embedding-retract-has-size : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                            â†’ (Ï : X â— Y)
                            â†’ is-embedding (section Ï)
                            â†’ X has-size ğ“¥
 embedding-retract-has-size {ğ“¤} {ğ“¥} {X} {Y} (r , s , Ï) Îµ =
  (Î£ y ê‰ Y , âˆ¥ s (r y) â‰¡ y âˆ¥) ,
  fixed-points-of-embedding-retraction (r , s , Ï) Îµ

\end{code}
