Tom de Jong, 16 February 2020 -

Retracts of maps.

f is a retract of g if there is a commutative diagram:

X --s--> W --r--> X # composition is id
|        |        |
f        g        f
|        |        |
âˆ¨       âˆ¨        âˆ¨
Y --u--> Z --v--> Y # composition is id

This could be developed further, including notation for composing such retracts
of maps. But the following suffices for our (current) purposes.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module UF-MapRetracts where

open import SpartanMLTT
open import UF-Base
open import UF-Retracts
open import UF-Embeddings
open import UF-Equiv
open import UF-EquivalenceExamples
open import UF-Subsingletons

module _
        {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {W : ğ“¦ Ì‡ } {Z : ğ“£ Ì‡ }
       where

 map-retract_of_ : (X â†’ Y) â†’ (W â†’ Z) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦ âŠ” ğ“£ Ì‡
 map-retract_of_ f g = Î£ s ê‰ (X â— W) , Î£ t ê‰ (Y â— Z) ,
  g âˆ˜ section s âˆ¼ section t âˆ˜ f
  Ã—
  f âˆ˜ retraction s âˆ¼ retraction t âˆ˜ g

 _â—â‚_ : (X â†’ Y) â†’ (W â†’ Z) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦ âŠ” ğ“£ Ì‡
 f â—â‚ g = map-retract f of g

 infix 0 _â—â‚_

 domains-retract : {f : X â†’ Y} {g : W â†’ Z} â†’ f â—â‚ g â†’ X â— W
 domains-retract (s , t , c , d) = s

 codomains-retract : {f : X â†’ Y} {g : W â†’ Z} â†’ f â—â‚ g â†’ Y â— Z
 codomains-retract (s , t , c , d) = t

 lhs-commutes : {f : X â†’ Y} {g : W â†’ Z} (r : f â—â‚ g)
              â†’ g âˆ˜ section (domains-retract r)
              âˆ¼ section (codomains-retract r) âˆ˜ f
 lhs-commutes (s , t , c , d) = c

 rhs-commutes : {f : X â†’ Y} {g : W â†’ Z} (r : f â—â‚ g)
              â†’ f âˆ˜ retraction (domains-retract r)
              âˆ¼ retraction (codomains-retract r) âˆ˜ g
 rhs-commutes (s , t , c , d) = d

\end{code}

If f is a retract of g, are the fibers of f retracts (in a suitable sense) of
the fibers of g?

As a first step, we record the following fact.

\begin{code}

 map-retract-gives-fiber-retract : {f : X â†’ Y} {g : W â†’ Z} â†’ (mr : f â—â‚ g)
                                 â†’ (y : Y)
                                 â†’ fiber f y
                                 â— fiber (g âˆ˜ section (domains-retract mr) âˆ˜
                                          retraction (domains-retract mr))
                                         (section (codomains-retract mr) y)
 map-retract-gives-fiber-retract {f} {g}
  ((r , s , rs) , ((u , v , vu) , c , d)) y =
   fiber f y               â—âŸ¨ Î£-section-retract (u , v , vu) f y âŸ©
   fiber (v âˆ˜ f) (v y)     â—âŸ¨ equiv-retract-r (âˆ¼-fiber-â‰ƒ c (v y)) âŸ©
   fiber (g âˆ˜ s) (v y)     â—âŸ¨ Î£-reindex-retract r (s , rs) âŸ©
   fiber (g âˆ˜ s âˆ˜ r) (v y) â—€

\end{code}

At the price of adding a coherence condition and the assumption that v (in the
diagram above) is an embedding, we can do better.

For notational purposes, we use some module parameters to record and name all of
the data of the map retract.

\begin{code}

 module _
         {f : X â†’ Y}
         {g : W â†’ Z}
         (s : X â†’ W)
         (r : W â†’ X)
         (a : (x : X) â†’ r (s x) â‰¡ x)
         (v : Y â†’ Z)
         (u : Z â†’ Y)
         (b : (y : Y) â†’ u (v y) â‰¡ y)
         (c : (x : X) â†’ g (s x) â‰¡ v (f x))
         (d : (w : W) â†’ f (r w) â‰¡ u (g w))
        where

  map-retract-gives-fiber-retract' : is-embedding v
                                   â†’ ((x : X) â†’ ap f (a x â»Â¹) âˆ™ d (s x)
                                                â‰¡ b (f x) â»Â¹ âˆ™ ap u (c x â»Â¹))
                                   â†’ (y : Y)
                                   â†’ fiber f y
                                   â— fiber g (v y)
  map-retract-gives-fiber-retract' Îµ coh y =
   fiber f y           â—âŸ¨ Î£-section-retract (u , v , b) f y âŸ©
   fiber (v âˆ˜ f) (v y) â—âŸ¨ equiv-retract-r (âˆ¼-fiber-â‰ƒ c (v y)) âŸ©
   fiber (g âˆ˜ s) (v y) â—âŸ¨ Ï , (Ïƒ , ÏÏƒ) âŸ©
   fiber g (v y)       â—€
    where
     Ï : fiber g (v y) â†’ fiber (g âˆ˜ s) (v y)
     Ï (w , q) = (r w) , p
      where
       p = g (s (r w)) â‰¡âŸ¨ c (r w) âŸ©
           v (f (r w)) â‰¡âŸ¨ ap v p' âŸ©
           v y         âˆ
        where
         p' : f (r w) â‰¡ y
         p' = f (r w) â‰¡âŸ¨ d w âŸ©
              u (g w) â‰¡âŸ¨ ap u q âŸ©
              u (v y) â‰¡âŸ¨ b y âŸ©
              y       âˆ
     Ïƒ : fiber (g âˆ˜ s) (v y) â†’ fiber g (v y)
     Ïƒ (x , p) = s x , p
     ÏÏƒ : (xp : fiber (g âˆ˜ s) (v y)) â†’ Ï (Ïƒ xp) â‰¡ xp
     ÏÏƒ (x , p) = to-Î£-â‰¡ ((a x) , Î³)
      where
       p' : g (s (r (s x))) â‰¡ v y
       p' = prâ‚‚ (Ï (s x , p))
       Î³ = transport (Î» - â†’ g (s -)â‰¡ v y) (a x) p'  â‰¡âŸ¨ I âŸ©
           ap (g âˆ˜ s) (a x â»Â¹) âˆ™ p'                 â‰¡âŸ¨ by-definition âŸ©
           ap (g âˆ˜ s) (a x â»Â¹) âˆ™ (c (r (s x)) âˆ™ pâ‚) â‰¡âŸ¨ II âŸ©
           ap (g âˆ˜ s) (a x â»Â¹) âˆ™ c (r (s x)) âˆ™ pâ‚   â‰¡âŸ¨ III âŸ©
           c x âˆ™ ap (v âˆ˜ f) (a x â»Â¹) âˆ™ pâ‚           â‰¡âŸ¨ IV âŸ©
           c x âˆ™ (ap (v âˆ˜ f) (a x â»Â¹) âˆ™ pâ‚)         â‰¡âŸ¨ ap (Î» - â†’ c x âˆ™ -) â„ âŸ©
           c x âˆ™ (c x â»Â¹ âˆ™ p)                       â‰¡âŸ¨ V âŸ©
           c x âˆ™ c x â»Â¹ âˆ™ p                         â‰¡âŸ¨ VI âŸ©
           refl âˆ™ p                                 â‰¡âŸ¨ refl-left-neutral âŸ©
           p                                        âˆ
        where
         pâ‚‚ : f (r (s x)) â‰¡ y
         pâ‚‚ = d (s x) âˆ™ (ap u p âˆ™ b y)
         pâ‚ : v (f (r (s x))) â‰¡ v y
         pâ‚ = ap v pâ‚‚
         I   = transport-fiber (g âˆ˜ s) (a x) p'
         II  = (âˆ™assoc (ap (g âˆ˜ s) (a x â»Â¹)) (c (r (s x))) pâ‚) â»Â¹
         III = ap (Î» - â†’ - âˆ™ pâ‚) (homotopies-are-natural (g âˆ˜ s) (v âˆ˜ f) c
                {x} {r (s x)} {a x â»Â¹}) â»Â¹
         IV  = âˆ™assoc (c x) (ap (v âˆ˜ f) (a x â»Â¹)) pâ‚
         V   = (âˆ™assoc (c x) (c x â»Â¹) p) â»Â¹
         VI  = ap (Î» - â†’ - âˆ™ p) ((right-inverse (c x)) â»Â¹)
         â„ = ap (v âˆ˜ f) (a x â»Â¹) âˆ™ pâ‚       â‰¡âŸ¨ I' âŸ©
             ap v (ap f (a x â»Â¹)) âˆ™ pâ‚      â‰¡âŸ¨ by-definition âŸ©
             ap v (ap f (a x â»Â¹)) âˆ™ ap v pâ‚‚ â‰¡âŸ¨ (ap-âˆ™ v (ap f (a x â»Â¹)) pâ‚‚) â»Â¹ âŸ©
             ap v (ap f (a x â»Â¹) âˆ™ pâ‚‚)      â‰¡âŸ¨ ap (ap v) h âŸ©
             ap v q                         â‰¡âŸ¨ II' âŸ©
             c x â»Â¹ âˆ™ p                     âˆ
          where
           Îµ' : is-equiv (ap v {f x} {y})
           Îµ' = embedding-embedding' v Îµ (f x) y
           q : f x â‰¡ y
           q = back-eqtofun ((ap v) , Îµ')
               ((c x â»Â¹) âˆ™ p)
           I'  = ap (Î» - â†’ - âˆ™ pâ‚) ((ap-ap f v (a x â»Â¹)) â»Â¹)
           II' = inverse-is-section (ap v) Îµ' (c x â»Â¹ âˆ™ p)
           h = ap f (a x â»Â¹) âˆ™ pâ‚‚                            â‰¡âŸ¨ by-definition âŸ©
               ap f (a x â»Â¹) âˆ™ (d (s x) âˆ™ (ap u p âˆ™ b y))    â‰¡âŸ¨ Iâ‚‚ âŸ©
               ap f (a x â»Â¹) âˆ™ d (s x) âˆ™ (ap u p âˆ™ b y)      â‰¡âŸ¨ IIâ‚‚ âŸ©
               b (f x) â»Â¹ âˆ™ ap u (c x â»Â¹) âˆ™ (ap u p âˆ™ b y)   â‰¡âŸ¨ IIIâ‚‚ âŸ©
               b (f x) â»Â¹ âˆ™ (ap u (c x â»Â¹) âˆ™ (ap u p âˆ™ b y)) â‰¡âŸ¨ IVâ‚‚ âŸ©
               b (f x) â»Â¹ âˆ™ (ap u (c x â»Â¹) âˆ™ ap u p âˆ™ b y)   â‰¡âŸ¨ Vâ‚‚ âŸ©
               b (f x) â»Â¹ âˆ™ (ap u (c x â»Â¹ âˆ™ p) âˆ™ b y)        â‰¡âŸ¨ VIâ‚‚ âŸ©
               b (f x) â»Â¹ âˆ™ ap u (c x â»Â¹ âˆ™ p) âˆ™ b y          â‰¡âŸ¨ VIIâ‚‚ âŸ©
               b (f x) â»Â¹ âˆ™ ap u (ap v q) âˆ™ b y              â‰¡âŸ¨ VIIIâ‚‚ âŸ©
               b (f x) â»Â¹ âˆ™ ap (u âˆ˜ v) q âˆ™ b y               â‰¡âŸ¨ IXâ‚‚ âŸ©
               ap id q                                       â‰¡âŸ¨ Xâ‚‚ âŸ©
               q                                             âˆ
            where
             Iâ‚‚    = (âˆ™assoc (ap f (a x â»Â¹)) (d (s x)) (ap u p âˆ™ b y)) â»Â¹
             IIâ‚‚   = ap (Î» - â†’ - âˆ™ (ap u p âˆ™ b y)) (coh x)
             IIIâ‚‚  = âˆ™assoc (b (f x) â»Â¹) (ap u (c x â»Â¹)) (ap u p âˆ™ b y)
             IVâ‚‚   = ap (Î» - â†’ b (f x) â»Â¹ âˆ™ -)
                     ((âˆ™assoc (ap u (c x â»Â¹)) (ap u p) (b y)) â»Â¹)
             Vâ‚‚    = ap (Î» - â†’ b (f x) â»Â¹ âˆ™ (- âˆ™ b y)) ((ap-âˆ™ u (c x â»Â¹) p) â»Â¹)
             VIâ‚‚   = (âˆ™assoc (b (f x) â»Â¹) (ap u (c x â»Â¹ âˆ™ p)) (b y)) â»Â¹
             VIIâ‚‚  = ap (Î» - â†’ b (f x) â»Â¹ âˆ™ ap u - âˆ™ b y) (II' â»Â¹)
             VIIIâ‚‚ = ap (Î» - â†’ b (f x) â»Â¹ âˆ™ - âˆ™ b y) (ap-ap v u q)
             IXâ‚‚   = homotopies-are-natural'' (u âˆ˜ v) id b {f x} {y} {q}
             Xâ‚‚    = (ap-id-is-id q) â»Â¹

\end{code}

We can get the desired retract without the embedding assumption, see Lemma 4.7.3
of the HoTT book.

It is noteworthy to mention that the coherence law above (which I
"rediscovered") is part of the definition of a retract in Definition 4.7.2 of
the HoTT book.

We use the same naming as there.

A --s--> X --r--> A # composition is id
|        |        |
g        f        g
|        |        |
âˆ¨        âˆ¨        âˆ¨
B --s'-> Y --r'-> B # composition is id

\begin{code}

module _
        {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } {X : ğ“¤' Ì‡ } {Y : ğ“¥' Ì‡ }
        {f : X â†’ Y}
        {g : A â†’ B}
        (s : A â†’ X)
        (r : X â†’ A)
        (s' : B â†’ Y)
        (r' : Y â†’ B)
        (R : (a : A) â†’ r (s a) â‰¡ a)
        (R' : (b : B) â†’ r' (s' b) â‰¡ b)
        (L : (a : A) â†’ f (s a) â‰¡ s' (g a))
        (K : (x : X) â†’ g (r x) â‰¡ r' (f x))
        (H : (a : A) â†’ K (s a) âˆ™ ap r' (L a) â‰¡ ap g (R a) âˆ™ R' (g a) â»Â¹)
       where

 map-retract-gives-fiber-retract'' : (b : B) â†’ fiber g b â— fiber f (s' b)
 map-retract-gives-fiber-retract'' b = Ï , Ïƒ , ÏÏƒ
  where
   Ïƒ : fiber g b â†’ fiber f (s' b)
   Ïƒ (a , p)  = (s a) , (f (s a)  â‰¡âŸ¨ L a âŸ©
                        s' (g a) â‰¡âŸ¨ ap s' p âŸ©
                        s' b     âˆ)
   Ï : fiber f (s' b) â†’ fiber g b
   Ï (x , q) = (r x) , (g (r x)   â‰¡âŸ¨ K x âŸ©
                        r' (f x)  â‰¡âŸ¨ ap r' q âŸ©
                        r' (s' b) â‰¡âŸ¨ R' b âŸ©
                        b         âˆ)
   ÏÏƒ : (w : fiber g b) â†’ Ï (Ïƒ w) â‰¡ w
   ÏÏƒ (a , refl) = to-Î£-â‰¡ (R a , Î³)
    where
     Î³ = transport (Î» - â†’ g - â‰¡ g a) (R a) p                     â‰¡âŸ¨ i âŸ©
         ap g (R a â»Â¹) âˆ™ p                                       â‰¡âŸ¨ ii âŸ©
         ap g (R a â»Â¹) âˆ™ (K (s a) âˆ™ (ap r' (L a) âˆ™ R' b))        â‰¡âŸ¨ iii âŸ©
         ap g (R a â»Â¹) âˆ™ (K (s a) âˆ™ ap r' (L a) âˆ™ R' b)          â‰¡âŸ¨ iv âŸ©
         ap g (R a â»Â¹) âˆ™ (ap g (R a) âˆ™ R' (g a) â»Â¹ âˆ™ R' (g a))   â‰¡âŸ¨ v âŸ©
         ap g (R a â»Â¹) âˆ™ (ap g (R a) âˆ™ (R' (g a) â»Â¹ âˆ™ R' (g a))) â‰¡âŸ¨ vi âŸ©
         ap g (R a â»Â¹) âˆ™ ap g (R a)                              â‰¡âŸ¨ vii âŸ©
         ap g (R a) â»Â¹ âˆ™ ap g (R a)                              â‰¡âŸ¨ viii âŸ©
         refl                                                    âˆ
      where
       p : g (r (s a)) â‰¡ g a
       p = K (s a) âˆ™ (ap r' (L a âˆ™ ap s' refl) âˆ™ R' b)
       i    = transport-fiber g (R a) p
       ii   = by-definition
       iii  = ap (Î» - â†’ ap g (R a â»Â¹) âˆ™ -)
             ((âˆ™assoc (K (s a)) (ap r' (L a)) (R' b)) â»Â¹)
       iv   = ap (Î» - â†’ ap g (R a â»Â¹) âˆ™ (- âˆ™ R' b)) (H a)
       v    = ap (Î» - â†’ ap g (R a â»Â¹) âˆ™ -)
             (âˆ™assoc (ap g (R a)) (R' (g a) â»Â¹) (R' (g a)))
       vi   = ap (Î» - â†’ ap g (R a â»Â¹) âˆ™ (ap g (R a) âˆ™ -))
              (left-inverse (R' (g a)))
       vii  = ap (Î» - â†’ - âˆ™ ap g (R a)) ((ap-sym g (R a)) â»Â¹)
       viii = left-inverse (ap g (R a))

\end{code}

Moreover, if s and s' are embeddings, then so is Ïƒ.

\begin{code}

 fiber-retract-section-is-embedding : is-embedding s
                                    â†’ is-embedding s'
                                    â†’ (b : B)
                                    â†’ is-embedding (section
                                       (map-retract-gives-fiber-retract'' b))
 fiber-retract-section-is-embedding Îµ Îµ' b = embedding-criterion' Ïƒ Î³
  where
   Ïƒ : fiber g b â†’ fiber f (s' b)
   Ïƒ = section (map-retract-gives-fiber-retract'' b)
   Ïƒâ‚‚ : (a : A) (p : g a â‰¡ b) â†’ f (s a) â‰¡ s' b
   Ïƒâ‚‚ a p = L a âˆ™ ap s' p
   Î³ : (u v : fiber g b) â†’ (Ïƒ u â‰¡ Ïƒ v) â‰ƒ (u â‰¡ v)
   Î³ (a , p) (a' , p') =
    (Ïƒ (a , p) â‰¡ Ïƒ (a' , p'))                                â‰ƒâŸ¨ Î£-â‰¡-â‰ƒ âŸ©
    (Î£ q ê‰ s a â‰¡ s a' , transport T q (Ïƒâ‚‚ a p) â‰¡ (Ïƒâ‚‚ a' p')) â‰ƒâŸ¨ i âŸ©
    (Î£ q ê‰ s a â‰¡ s a' , ap f (q â»Â¹) âˆ™ Ïƒâ‚‚ a p â‰¡ Ïƒâ‚‚ a' p')     â‰ƒâŸ¨ ii âŸ©
    (Î£ q ê‰ a â‰¡ a' , ap f (ap s q â»Â¹) âˆ™ Ïƒâ‚‚ a p â‰¡ Ïƒâ‚‚ a' p')    â‰ƒâŸ¨ iii âŸ©
    (Î£ q ê‰ a â‰¡ a' , ap s' (ap g (q â»Â¹) âˆ™ p) â‰¡ ap s' p')      â‰ƒâŸ¨ iv âŸ©
    (Î£ q ê‰ a â‰¡ a' , ap g (q â»Â¹) âˆ™ p â‰¡ p')                    â‰ƒâŸ¨ v âŸ©
    (Î£ q ê‰ a â‰¡ a' , transport (Î» - â†’ g - â‰¡ b) q p â‰¡ p')      â‰ƒâŸ¨ â‰ƒ-sym Î£-â‰¡-â‰ƒ âŸ©
    (a , p â‰¡ a' , p')                                        â– 
     where
      T : X â†’ ğ“¥' Ì‡
      T x = f x â‰¡ s' b
      i   = Î£-cong (Î» q â†’ idtoeq _ _
            (ap (Î» - â†’ - â‰¡ Ïƒâ‚‚ a' p') (transport-fiber f q (Ïƒâ‚‚ a p))))
      ii  = â‰ƒ-sym (Î£-change-of-variables (Î» q â†’ ap f (q â»Â¹) âˆ™ Ïƒâ‚‚ a p â‰¡ Ïƒâ‚‚ a' p')
            (ap s) (embedding-embedding' s Îµ a a'))
      iv  = Î£-cong (Î» q â†’ â‰ƒ-sym ((ap (ap s')) , embedding-embedding' (ap s')
            (equivs-are-embeddings (ap s') (embedding-embedding' s' Îµ' (g a') b))
            (ap g (q â»Â¹) âˆ™ p) p'))
      v   = Î£-cong (Î» q â†’ idtoeq _ _ (ap (Î» - â†’ - â‰¡ p')
            (transport-fiber g q p) â»Â¹))
      iii = Î£-cong (Î» q â†’ idtoeq _ _ (h q))
       where
        h : (q : a â‰¡ a')
          â†’ (ap f (ap s q â»Â¹) âˆ™ Ïƒâ‚‚ a p â‰¡ Ïƒâ‚‚ a' p')
          â‰¡ (ap s' (ap g (q â»Â¹) âˆ™ p) â‰¡ ap s' p')
        h refl = (ap f (ap s refl â»Â¹) âˆ™ Ïƒâ‚‚ a p â‰¡ Ïƒâ‚‚ a' p') â‰¡âŸ¨ refl âŸ©
                 (refl âˆ™ Ïƒâ‚‚ a p â‰¡ Ïƒâ‚‚ a' p')                â‰¡âŸ¨ i' âŸ©
                 (Ïƒâ‚‚ a p â‰¡ Ïƒâ‚‚ a p')                        â‰¡âŸ¨ by-definition âŸ©
                 (L a âˆ™ ap s' p â‰¡ L a âˆ™ ap s' p')          â‰¡âŸ¨ cancel-left-â‰¡ âŸ©
                 (ap s' p â‰¡ ap s' p')                      â‰¡âŸ¨ ii' âŸ©
                 (ap s' (refl âˆ™ p) â‰¡ ap s' p')             â‰¡âŸ¨ refl âŸ©
                 (ap s' (ap g (refl â»Â¹) âˆ™ p) â‰¡ ap s' p')   âˆ
         where
          i'  = ap (Î» - â†’ - â‰¡ Ïƒâ‚‚ a' p') refl-left-neutral
          ii' = ap (Î» - â†’ ap s' - â‰¡ ap s' p')
                (refl-left-neutral {ğ“¥} {B} {g a} {b} {p}) â»Â¹

\end{code}
